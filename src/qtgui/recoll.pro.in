TEMPLATE	= app
LANGUAGE	= C++

CONFIG	+= qt warn_on thread release debug

HEADERS	+= \
	advsearch_w.h \
	spell_w.h \
	preview_w.h \
	rclmain_w.h \
	reslist.h \
	sort_w.h \
	searchclause_w.h \
	ssearch_w.h \
	uiprefs_w.h \
        viewaction_w.h


SOURCES	+= \
	guiutils.cpp \
	idxthread.cpp \
	main.cpp \
	plaintorich.cpp \
	rclmain_w.cpp \
	reslist.cpp \
        advsearch_w.cpp \
	spell_w.cpp \
        preview_w.cpp \
        sort_w.cpp \
	searchclause_w.cpp \
        ssearch_w.cpp \
        uiprefs_w.cpp \
        viewaction_w.cpp

FORMS	= \
	advsearch.ui \
	spell.ui \
	preview.ui \
	rclmain.ui \
	sort.ui \
	ssearchb.ui \
	uiprefs.ui \
	viewaction.ui

IMAGES	= \
	images/asearch.png \
	images/d_firstpage.png \
	images/d_nextpage.png \
	images/d_prevpage.png \
	images/d_spell.png \
	images/firstpage.png \
	images/history.png \
	images/nextpage.png \
	images/prevpage.png \
	images/sortparms.png \
	images/spell.png 

unix {
  UI_DIR = .ui
  MOC_DIR = .moc
  OBJECTS_DIR = .obj

  DEFINES += RECOLL_DATADIR=\"@QTRECOLL_DATADIR@\"
  LIBS += ../lib/librcl.a ../bincimapmime/libmime.a \
            $(BSTATIC) @LIBXAPIAN@ @LIBICONV@ $(BDYNAMIC) \
           -lz

  INCLUDEPATH += ../common ../index ../internfile ../query ../unac \
	      ../utils ../aspell ../rcldb
  POST_TARGETDEPS = ../lib/librcl.a
}

UNAME = $$system(uname -s)
contains( UNAME, [lL]inux ) {
          LIBS -= -liconv
}
contains( UNAME, SunOS ) {
          LIBS += -ldl
}

TRANSLATIONS = i18n/recoll_fr.ts i18n/recoll_ru.ts i18n/recoll_uk.ts \
    i18n/recoll_it.ts
